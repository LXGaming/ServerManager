import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.jfrog.bintray"
    id "java"
    id "java-library"
    id "maven-publish"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "servermanager-api"
version = version

configurations {
    provided {
        compile.extendsFrom(provided)
    }

    compileJava.dependsOn("filterJava")
}

repositories {
    jcenter()
}

dependencies {
    api("com.google.code.gson:gson:2.8.5")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["mavenPublication"]
    pkg {
        repo = "maven"
        name = "ServerManager"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/LXGaming/ServerManager.git"
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

bintrayUpload {
    onlyIf { System.getenv("BINTRAY_KEY") != null }
    onlyIf { System.getenv("BINTRAY_USER") != null }
    onlyIf { System.getenv("TRAVIS_BUILD_NUMBER") != null }
    onlyIf { System.getenv("TRAVIS_JOB_NUMBER") ==~ /\d+\.1$/ }
    onlyIf { System.getenv("TRAVIS_PULL_REQUEST") == "false" }
}

compileJava {
    doFirst {
        source = filterJava.outputs
    }
}

jar {
    from {
        (configurations.compile - configurations.provided).findAll({
            it.isDirectory() || it.name.endsWith(".jar")
        }).collect({
            it.isDirectory() ? it : zipTree(it)
        })
    }
}

javadoc {
    failOnError = false
}

processResources {
    from("../LICENSE")
    rename("LICENSE", "LICENSE-ServerManager")
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

task filterJava(type: Sync) {
    from(sourceSets.main.java)
    into("$buildDir/tmp/$name")

    filter(ReplaceTokens, tokens: [version: version])
}