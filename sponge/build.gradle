buildscript {
    repositories {
        jcenter()
        maven {
            name = "minecraftforge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "spongepowered"
            url = "https://repo.spongepowered.org/maven"
        }
    }

    dependencies {
        classpath("com.github.jengelman.gradle.plugins:shadow:2.0.4")
        classpath("net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT")
        classpath("org.spongepowered:mixingradle:0.6-SNAPSHOT")
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "java"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "signing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "nz.co.lolnet"
archivesBaseName = "ServerManager-Sponge"
version = "2.0.0-DEV"

minecraft {
    version = "1.12.2-14.23.4.2705"
    runDir = "run"
    mappings = "snapshot_20171003"
    makeObfSourceJar = false
}

mixin {
    defaultObfuscationEnv = searge
    disableRefMapWarning = true
    add(sourceSets.main, "mixins.servermanager.refmap.json")
}

configurations {
    provided {
        compile.extendsFrom(provided)
    }

    extractAnnotationsJar.enabled = false
    jar.enabled = false
    jar.dependsOn("shadowJar")
}

repositories {
    jcenter()
    maven {
        name = "spongepowered"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    shadow(project(path: ":common")) {
        exclude(module: "gson")
    }

    annotationProcessor("org.spongepowered:spongecommon:7.1.0-SNAPSHOT:dev")
    provided("org.spongepowered:spongecommon:7.1.0-SNAPSHOT:dev")
    shadow("redis.clients:jedis:2.9.0")
}

shadowJar {
    classifier = null
    configurations = [project.configurations.shadow]
    relocate("org.apache.commons.pool2", "nz.co.lolnet.servermanager.lib.commonspool2")
    relocate("redis.clients.jedis", "nz.co.lolnet.servermanager.lib.jedis")
    relocate("redis.clients.util", "nz.co.lolnet.servermanager.lib.jedisutil")
}

reobf {
    shadowJar {}
}

jar {
    manifest {
        attributes(
                "FMLCorePluginContainsFMLMod": true,
                "ForceLoadAsMod": true,
                "MixinConfigs": "mixins.servermanager.core.json",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
        )
    }

    from {
        (configurations.compile - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    exclude("dummyThing")
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-" + archivesBaseName)
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        jar.outputs.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}